buildscript {
    repositories {
        mavenLocal()
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'maven-publish'
    id 'net.minecrell.licenser' version '0.4.1'
    id 'org.ajoberstar.grgit' version '3.1.1'
    id 'com.github.ben-manes.versions' version '0.24.0'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
}

group 'net.minecraftforge'
version = gitVersion()
targetCompatibility = sourceCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'
def gitVersion() {
    def raw = grgit.describe(longDescr: true, tags:true)
    def desc = (raw == null ? 'unknown-offset-hash' : raw).split('-') as List
    def hash = desc.remove(desc.size() - 1)
    def offset = desc.remove(desc.size() - 1)
    def tag = desc.join('-')
    def branch = grgit.branch.current().name
    project.ext.SPEC_VERSION = tag
    return "${tag}.${offset}${t -> if (branch != 'master') t << '-' + branch}"
}

ext {
    CHANGELOG = rootProject.file('build/changelog.txt')
    MAIN_CLASS = 'net.minecraftforge.depigifier.Unpig'
    MANIFEST = manifest{
        attributes('Main-Class': MAIN_CLASS)
        attributes([
            'Specification-Title':      'DePigifier',
            'Specification-Vendor':     'Forge Development LLC',
            'Specification-Version':    project.version,
            'Implementation-Title':     'DePigifier',
            'Implementation-Version':   project.version,
            'Implementation-Vendor':    'Forge Development LLC'
        ] as LinkedHashMap, 'net/minecraftforge/depigifier/')
    }
}

repositories {
    mavenLocal()
    maven { url = 'https://files.minecraftforge.net/maven' }
    jcenter()
    mavenCentral()
}

test {
    useJUnitPlatform()
}

license {
    header = file('LICENSE-header.txt')
}

dependencies {
    testImplementation('org.junit.jupiter:junit-jupiter:5.5.+')
    testImplementation('org.powermock:powermock-core:2.0+')
    implementation('net.sf.jopt-simple:jopt-simple:5.0.4')
    implementation('com.machinezoo.noexception:noexception:1.3.4')
    implementation('org.slf4j:slf4j-api:1.7.28')
    implementation('org.slf4j:slf4j-simple:1.7.28')
    implementation('org.ow2.asm:asm:7.1')
}

shadowJar {
    classifier 'fatjar'
    manifest.from(MANIFEST)
}
build.dependsOn shadowJar
artifacts {
    archives shadowJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            if (CHANGELOG.exists()) {
                artifact(CHANGELOG) {
                    classifier = 'changelog'
                }
            }
            artifact jar
            artifact shadowJar
            pom {
                name = 'Depigifier'
                description = 'Remaps SRG files based on Proguard data'
                url = 'https://github.com/MinecraftForge/Depigifier'
                scm {
                    url = 'https://github.com/MinecraftForge/Depigifier'
                    connection = 'scm:git:git://github.com/MinecraftForge/Depigifier.git'
                    developerConnection = 'scm:git:git@github.com:MinecraftForge/Depigifier.git'
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/MinecraftForge/Depigifier/issues'
                }

                licenses {
                    license {
                        name = 'LGPLv2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                    }
                }

                developers {
                    developer {
                        id = 'cpw'
                        name = 'cpw'
                    }
                    developer {
                        id = 'LexManos'
                        name = 'LexManos'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            if (project.hasProperty('forgeMavenPassword')) {
                credentials {
                    username project.properties.forgeMavenUser
                    password project.properties.forgeMavenPassword
                }
                url 'https://files.minecraftforge.net/maven/manage/upload'
            } else {
                url 'file://' + rootProject.file('repo').getAbsolutePath()
            }
        }
    }
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

